# Day 2

```elixir
Mix.install([
  {:kino_benchee, git: "https://github.com/livebook-dev/kino_benchee"}
])
```

## Import Data

```elixir
data = File.read!("/Users/theodowling/Source/elixir/advent-of-code-2022/data/day2.txt")

test_input = """
A Y
B X
C Z
"""
```

## Part 1

### --- Day 2: Rock Paper Scissors ---

The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant Rock Paper Scissors tournament is already in progress.

Rock Paper Scissors is a game between two players. Each game contains many rounds; in each round, the players each simultaneously choose one of Rock, Paper, or Scissors using a hand shape. Then, a winner for that round is selected: Rock defeats Scissors, Scissors defeats Paper, and Paper defeats Rock. If both players choose the same shape, the round instead ends in a draw.

Appreciative of your help yesterday, one Elf gives you an encrypted strategy guide (your puzzle input) that they say will be sure to help you win. "The first column is what your opponent is going to play: A for Rock, B for Paper, and C for Scissors. The second column--" Suddenly, the Elf is called away to help with someone's tent.

The second column, you reason, must be what you should play in response: X for Rock, Y for Paper, and Z for Scissors. Winning every time would be suspicious, so the responses must have been carefully chosen.

The winner of the whole tournament is the player with the highest score. Your total score is the sum of your scores for each round. The score for a single round is the score for the shape you selected (1 for Rock, 2 for Paper, and 3 for Scissors) plus the score for the outcome of the round (0 if you lost, 3 if the round was a draw, and 6 if you won).

Since you can't be sure if the Elf is trying to help you or trick you, you should calculate the score you would get if you were to follow the strategy guide.

For example, suppose you were given the following strategy guide:

```
A Y
B X
C Z
```

This strategy guide predicts and recommends the following:

In the first round, your opponent will choose Rock (A), and you should choose Paper (Y). This ends in a win for you with a score of 8 (2 because you chose Paper + 6 because you won).
In the second round, your opponent will choose Paper (B), and you should choose Rock (X). This ends in a loss for you with a score of 1 (1 + 0).
The third round is a draw with both players choosing Scissors, giving you a score of 3 + 3 = 6.
In this example, if you were to follow the strategy guide, you would get a total score of 15 (8 + 1 + 6).

*What would your total score be if everything goes exactly according to your strategy guide?*

```elixir
defmodule Part1 do
  @mapping %{"X" => :rock, "Y" => :paper, "Z" => :scissors}
  @opponent %{"A" => :rock, "B" => :paper, "C" => :scissors}

  def main(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(fn line ->
      <<op::binary-size(1), " ", elf::binary-size(1)>> = line
      outcome(@opponent[op], @mapping[elf])
    end)
    |> Enum.sum()
  end

  def outcome(a, :rock), do: 1 + game_outcome(a, :rock)
  def outcome(a, :paper), do: 2 + game_outcome(a, :paper)
  def outcome(a, :scissors), do: 3 + game_outcome(a, :scissors)

  def game_outcome(a, a), do: 3
  def game_outcome(:rock, :paper), do: 6
  def game_outcome(:paper, :scissors), do: 6
  def game_outcome(:scissors, :rock), do: 6
  def game_outcome(_, _), do: 0
end

Part1.main(test_input) == 15
Part1.main(data)
```

## Part 2

### --- Part Two ---

The Elf finishes helping with the tent and sneaks back over to you. "Anyway, the second column says how the round needs to end: X means you need to lose, Y means you need to end the round in a draw, and Z means you need to win. Good luck!"

The total score is still calculated in the same way, but now you need to figure out what shape to choose so the round ends as indicated. The example above now goes like this:

In the first round, your opponent will choose Rock (A), and you need the round to end in a draw (Y), so you also choose Rock. This gives you a score of 1 + 3 = 4.
In the second round, your opponent will choose Paper (B), and you choose Rock so you lose (X) with a score of 1 + 0 = 1.
In the third round, you will defeat your opponent's Scissors with Rock for a score of 1 + 6 = 7.
Now that you're correctly decrypting the ultra top secret strategy guide, you would get a total score of 12.

*Following the Elf's instructions for the second column, what would your total score be if everything goes exactly according to your strategy guide?*

```elixir
defmodule Part2 do
  @result %{"X" => :lose, "Y" => :draw, "Z" => :win}
  @opponent %{"A" => :rock, "B" => :paper, "C" => :scissors}
  @points %{:rock => 1, :paper => 2, :scissors => 3}

  def main(input) do
    input
    |> String.split("\n", trim: true)
    |> Enum.map(fn line ->
      <<op::binary-size(1), " ", elf::binary-size(1)>> = line
      outcome(@opponent[op], @result[elf])
    end)
    |> Enum.sum()
  end

  def outcome(a, :win), do: 6 + point_for_chosen_item(a, :win)
  def outcome(a, :lose), do: 0 + point_for_chosen_item(a, :lose)
  def outcome(a, :draw), do: 3 + @points[a]

  def point_for_chosen_item(:rock, :win), do: @points[:paper]
  def point_for_chosen_item(:paper, :win), do: @points[:scissors]
  def point_for_chosen_item(:scissors, :win), do: @points[:rock]
  def point_for_chosen_item(:rock, :lose), do: @points[:scissors]
  def point_for_chosen_item(:paper, :lose), do: @points[:rock]
  def point_for_chosen_item(:scissors, :lose), do: @points[:paper]
end

Part2.main(test_input)
Part2.main(dat)
```

## Benchmarks

```elixir
defmodule BenchmarkTests do
  @data File.read!("/Users/theodowling/Source/elixir/advent-of-code-2022/data/day2.txt")

  def part1 do
    @data |> Part1.main()
  end

  def part2 do
    @data |> Part2.main()
  end
end

Benchee.run(
  %{
    "day2.part1" => &BenchmarkTests.part1/0,
    "day2.part2" => &BenchmarkTests.part2/0
  },
  time: 1,
  memory_time: 1,
  reduction_time: 1
)
```
